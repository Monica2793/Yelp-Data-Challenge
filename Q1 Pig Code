SET elephantbird.jsonloader.nestedLoad 'true';

A = LOAD './yelp_academic_dataset_business.json' USING com.twitter.elephantbird.pig.load.JsonLoader('-nestedLoad=true') AS (yelp: map[]);

business = FOREACH A GENERATE yelp#'categories' as categories, (int)yelp#'review_count' as review_count, yelp#'city' as city, 
yelp#'state' as state, (float)yelp#'latitude' as latitude, (float)yelp#'longitude' as longitude;

coordinates_business = FILTER business BY (latitude<49.384472) AND (latitude>24.520833) AND (longitude<-66.950) AND (longitude>-124.766667); 

us_business = FILTER coordinates_business BY NOT ( (state matches '.*ON.*') OR (state matches '.*QC.*') );

businesses = FOREACH us_business GENERATE FLATTEN(categories), city, state, review_count;

grouped = GROUP businesses BY (city,categories);

result = FOREACH grouped GENERATE group, SUM(businesses.review_count);

store result into './FinalQ14';